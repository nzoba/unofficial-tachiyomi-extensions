apply plugin: 'org.jmailen.kotlinter'

android {
    compileSdkVersion Config.compileSdk

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
        release {
            manifest.srcFile "AndroidManifest.xml"
        }
        debug {
            manifest.srcFile "AndroidManifest.xml"
        }
    }

    defaultConfig {
        minSdkVersion Config.minSdk
        targetSdkVersion Config.targetSdk
        applicationIdSuffix pkgNameSuffix
        versionCode extVersionCode
        versionName "$libVersion.$extVersionCode"
        setProperty("archivesBaseName", "tachiyomi-$pkgNameSuffix-v$versionName")
        manifestPlaceholders = [
                appName : "Tachiyomi: $extName",
                extClass: extClass,
                extFactory: project.ext.properties.getOrDefault("extFactory", ""),
                nsfw: project.ext.properties.getOrDefault("containsNsfw", false) ? 1 : 0,
        ]
    }

    signingConfigs {
        release {
            storeFile rootProject.file("signingkey.jks")
            storePassword System.getenv("KEY_STORE_PASSWORD")
            keyAlias System.getenv("ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }

    dependenciesInfo {
        includeInApk = false
    }

    buildFeatures {
        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":core")
}
apply from: "$rootDir/common-dependencies.gradle"

preBuild.dependsOn(lintKotlin)
lintKotlin.dependsOn(formatKotlin)
